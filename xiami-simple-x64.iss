; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "虾米音乐"
#define MyAppVersion "3.0.6"
#define MyAppPublisher "Alibaba music, Inc."
#define MyAppURL "http://www.xiami.com/"
#define MyAppExeName "虾米音乐.exe"
#define SourceDir "D:\desktop\x-xiami-desktop"
                                 
[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{73E58E31-4F03-4785-ADC2-0BEF2B12A7B4}
AppMutex=xiami-desktop
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}   
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DisableProgramGroupPage=yes
OutputDir=D:\xiami-build\Setup-Compiler\{#MyAppVersion}
OutputBaseFilename=虾米音乐-{#MyAppVersion}-x64-449E6FC
SetupIconFile={#SourceDir}\build\icon.ico                                 
Compression=lzma
SolidCompression=yes
                                                                     
[Languages]
Name: "chinesesimp"; MessagesFile: "compiler:Languages\ChineseSimplified.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "{#SourceDir}\dist\虾米音乐-win32-x64\虾米音乐.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourceDir}\dist\虾米音乐-win32-x64\content_resources_200_percent.pak"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourceDir}\dist\虾米音乐-win32-x64\content_shell.pak"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourceDir}\dist\虾米音乐-win32-x64\d3dcompiler_47.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourceDir}\dist\虾米音乐-win32-x64\ffmpeg.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourceDir}\dist\虾米音乐-win32-x64\icudtl.dat"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourceDir}\dist\虾米音乐-win32-x64\libEGL.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourceDir}\dist\虾米音乐-win32-x64\libGLESv2.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourceDir}\dist\虾米音乐-win32-x64\LICENSE"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourceDir}\dist\虾米音乐-win32-x64\LICENSES.chromium.html"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourceDir}\dist\虾米音乐-win32-x64\natives_blob.bin"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourceDir}\dist\虾米音乐-win32-x64\node.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourceDir}\dist\虾米音乐-win32-x64\snapshot_blob.bin"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourceDir}\dist\虾米音乐-win32-x64\ui_resources_200_percent.pak"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourceDir}\dist\虾米音乐-win32-x64\version"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourceDir}\dist\虾米音乐-win32-x64\xinput1_3.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourceDir}\dist\虾米音乐-win32-x64\resources\*"; DestDir: "{app}\\resources"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#SourceDir}\dist\虾米音乐-win32-x64\locales\*"; DestDir: "{app}\\locales"; Flags: ignoreversion recursesubdirs createallsubdirs

[InstallDelete]
Type: filesandordirs; Name:"{app}\resources"

[Icons]
Name: "{commonprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Code]
function IsAppRunning(const FileName : string): Boolean;
var
    FSWbemLocator: Variant;
    FWMIService   : Variant;
    FWbemObjectSet: Variant;
begin
    Result := false;
    FSWbemLocator := CreateOleObject('WBEMScripting.SWBEMLocator');
    FWMIService := FSWbemLocator.ConnectServer('', 'root\CIMV2', '', '');
    FWbemObjectSet := FWMIService.ExecQuery(Format('SELECT Name FROM Win32_Process Where Name="%s"',[FileName]));
    Result := (FWbemObjectSet.Count > 0);
    FWbemObjectSet := Unassigned;
    FWMIService := Unassigned;
    FSWbemLocator := Unassigned;
end;
 
function InitializeSetup(): Boolean;
begin
  if IsAppRunning( '{#MyAppExeName}' ) then
  begin
    MsgBox( '虾米音乐正在运行,请先关闭然后再运行安装程序.',
             mbError, MB_OK );
    Result := false;
  end
  else Result := true;
end;
 
function InitializeUninstall(): Boolean;
begin
  if IsAppRunning( '{#MyAppExeName}' ) then
  begin
    MsgBox( '虾米音乐正在运行,请先关闭然后再运行卸载程序.',
             mbError, MB_OK );
    Result := false;
  end
  else Result := true;
end;


[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

